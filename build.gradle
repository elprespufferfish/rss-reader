buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}
defaultTasks 'build'

apply plugin: 'com.android.application'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'com.neenbedankt.android-apt'

checkstyle {
    toolVersion = "6.11"
}
task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/google_checks.xml")
    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError true
        //warningsAsErrors true
    }

    packagingOptions {
      exclude 'LICENSE.txt'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    apt 'com.google.dagger:dagger-compiler:2.0.2'

    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.google.dagger:dagger:2.0.2'
    compile 'com.google.guava:guava:18.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'joda-time:joda-time:2.8.1'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'org.slf4j:slf4j-handroid:1.7.13'

    provided 'org.glassfish:javax.annotation:10.0-b28'

    androidTestCompile 'com.android.support:support-annotations:23.3.0'
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}

task findbugs(type: FindBugs, dependsOn: assembleDebug) {
    description 'Run FindBugs'
    group 'verification'

    classes = fileTree('build/intermediates/classes/debug/')
    source = fileTree('src/main/java')
    classpath = files()

    effort = 'max'

    excludeFilter = file('findbugs-exclude.xml')

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
